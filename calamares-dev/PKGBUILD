# Maintainer: TNE <TNE[at]EosOS[dot]org>
# Contributor: Philip MÃ¼ller <philm[at]manjaro[dog]org>

pkgname=calamares-eo
pkgver=3.4.1.r26.g3f6cd831f
_commit=3f6cd831f5e71eb3b0e1e8b60d0afe1ec989eff1
pkgrel=2
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
url="https://calamares.codeberg.page"
license=('LGPL')
conflicts=('calamares' 'calamares-dev')
provides=('calamares' 'calamares-dev')
replaces=('calamares-dev')
depends=('kconfig' 'kcoreaddons' 'kiconthemes' 'ki18n' 'solid' 'yaml-cpp' 'kpmcore'
  'boost-libs' 'ckbcomp' 'hwinfo' 'qt6-svg' 'polkit-qt6'
  'squashfs-tools' 'libpwquality' 'python' 'python-distutils-extra')
makedepends=('extra-cmake-modules' 'qt6-tools' 'qt6-translations' 'git' 'boost')

source=(
  git+https://codeberg.org/Calamares/calamares.git#commit=${_commit}
  git+https://github.com/sourcecorearch/core-pkgbuilds.git
  "calamares.desktop"
  "calamares_polkit"
  "49-nopasswd-calamares.rules"
)

sha256sums=(
  'SKIP'
  'SKIP'
  'SKIP'
  'c176b28007bd1c1f23d8dbb2c936fa54d0c01bacfb67290ddad597606c129df3'
  '56d85ff6bf860b9559b8c9f997ad9b1002f3fccc782073760eca505e3bddd176'
)

pkgver() {
  cd "$srcdir/calamares" || exit

  _regex='^set\(CALAMARES_VERSION ([0-9]+\.[0-9]+\.[0-9]+([^0-9].*)?)\)\s*$'
  _file='CMakeLists.txt'

  _line=$(
    grep -E "$_regex" "$_file" |
      head -1
  )
  _version=$(
    printf '%s' "$_line" | sed -E "s@$_regex@\1@;s@alpha@a@;s@beta@b@;s@-@.@"
  )
  _line=$(
    printf '%s' "$_line" |
      sed -E 's@\(@\\(@;s@\)@\\)@'
  )
  _commit=$(
    git log -G "$_line" -1 --pretty=oneline --no-color | sed 's@\ .*$@@'
  )
  _revision=$(
    git rev-list --count "$_commit"..HEAD
  )
  _hash=$(
    git rev-parse --short HEAD
  )

  printf '%s.r%s.g%s' \
    "$_version" \
    "$_revision" \
    "$_hash"
}

prepare() {
  cd ${srcdir}/calamares

  cp -rv "${srcdir}/core-pkgbuilds/calamares-dev/modules/bootloader" src/modules/
  cp -rv "${srcdir}/core-pkgbuilds/calamares-dev/modules/postcfg" src/modules/

  sed -i -e 's/etc\/sddm.conf/etc\/sddm.conf.d\/kde_settings.conf/' src/modules/displaymanager/main.py
}

build() {
  cd "${srcdir}/calamares" || exit

  mkdir -p build && cd build || exit
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBoost_NO_BOOST_CMAKE=ON \
    -DWITH_QT6=ON \
    -DWITH_APPSTREAM=OFF \
    -DBUILD_TESTING=OFF \
    -DSKIP_MODULES="webview interactiveterminal initramfs \
                              initramfscfg packagechooser packagechooserq \
                              dummyprocess dummypython dummycpp \
                              dummypythonqt services-openrc"
  make
}

package() {
  cd ${srcdir}/calamares/build
  DESTDIR="${pkgdir}" cmake --build . --target install
  install -Dm644 "${srcdir}/calamares.desktop" "$pkgdir/etc/xdg/autostart/calamares.desktop"
  install -Dm755 "${srcdir}/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
  install -Dm644 "${srcdir}/49-nopasswd-calamares.rules" "$pkgdir/etc/polkit-1/rules.d/49-nopasswd-calamares.rules"
  chmod 750 "$pkgdir/etc/polkit-1/rules.d"
}
